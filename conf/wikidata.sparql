# Get administrative areas (with some exemptions) in North Rhine-Westphalia with German labels

SELECT DISTINCT ?item ?itemLabel ?partOf 
                #(GROUP_CONCAT(DISTINCT ?hasPart ; separator = "; ") AS ?hasParts)
                WHERE {
  hint:Query hint:optimizer "None".
  { ?item wdt:P131* wd:Q1198;
        wdt:P31/wdt:P279* wd:Q387917. }
  UNION
  {  ?item wdt:P131* wd:Q1198 ;
       wdt:P31 wd:Q82794 }
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q192611 } # ohne Wahlkreise
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q20296613 } # Ohne Natur- und Landschaftsschutzgebiete
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q1643757 } # Ohne Landschaftsverbände
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q2520712 } # Ohne Verwaltungskooperationen 
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q478847 } # Ohne Ämter
  FILTER NOT EXISTS { ?item wdt:P31/wdt:P279* wd:Q2740635 } # Ohne Stadtbezirke
  SERVICE wikibase:label {  bd:serviceParam wikibase:language "de" }
  OPTIONAL {?item wdt:P131 ?partOf .}
  #OPTIONAL {?item wdt:P150 ?hasPart .}
}
GROUP BY ?item ?itemLabel ?partOf