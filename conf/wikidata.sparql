# Get entries with NWBib-ID plus label, dissolution data, partOf information and ags/ks/rs
SELECT DISTINCT
?item
?itemLabel
?dissolutionDate
?ags
?ks
?rs
?partOf
?nwbibId
?gnd
(group_concat(distinct ?stadtbezirkTyp; separator = ", ") as ?isStadtbezirk)
WHERE {
  ?item wdt:P6814 ?nwbibId.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "de" }
  OPTIONAL { ?item wdt:P31 ?stadtbezirkTyp .
             ?stadtbezirkTyp wdt:P279* wd:Q2740635 . }
  OPTIONAL { ?item wdt:P227 ?gnd }
  OPTIONAL { ?item wdt:P576 ?dissolutionDate }
  OPTIONAL { ?item wdt:P439 ?ags
            FILTER NOT EXISTS { ?item wdt:P576 ?dissolutionDate } }
  OPTIONAL { ?item wdt:P440 ?ks
            FILTER NOT EXISTS { ?item wdt:P576 ?dissolutionDate } }
  OPTIONAL { ?item wdt:P1388 ?rs
            FILTER NOT EXISTS { ?item wdt:P576 ?dissolutionDate } }
  OPTIONAL { ?item p:P6814 [ pq:P4900 ?broader ] .
             ?broader wdt:P6814 ?partOf }
}

GROUP BY ?item ?itemLabel ?dissolutionDate ?ags ?ks ?rs ?partOf ?nwbibId ?gnd
