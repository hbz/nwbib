# Get administrative areas in North Rhine-Westphalia with German labels
SELECT DISTINCT
?item
?itemLabel
?partOf
?gnd
?dissolutionDate
(group_concat(distinct ?hasPart; separator = ", ") as ?parts)
WHERE {
           { ?item wdt:P131* wd:Q1198 . }
      UNION
           { ?item p:P131 [ ps:P131 wd:Q1198 ] . }
           { ?item p:P31 [ ps:P31 wd:Q829277 ] . } # Regierungsbezirk in NRW
       UNION
           { ?item p:P31 [ ps:P31 wd:Q106658 ] . } # Landkreis
       UNION
           { ?item p:P31 [ ps:P31 wd:Q5283531 ] . } # Landkreis in Preußen
       UNION
           { ?item p:P31 [ ps:P31 wd:Q262166 ] . } # Gemeinde
       UNION
           { ?item p:P31 [ ps:P31 wd:Q22865 ] . } # kreisfreie Stadt
       UNION
           { ?item p:P31 [ ps:P31 wd:Q253019 ]. } # Ortsteil
       UNION
           { ?item p:P31 [ ps:P31 wd:Q2983893 ]. } # Stadtteil
       UNION
           { ?item p:P31 [ ps:P31 wd:Q42744322 ]. } # Stadtgemeinde Deutschlands
       UNION
           { ?item p:P31 [ ps:P31 wd:Q134626 ]. } # Kreisstadt
       UNION
           { ?item p:P31 [ ps:P31 wd:Q448801 ]. } # Große Kreisstadt
       UNION
           { ?item p:P31 [ ps:P31 wd:Q1548518 ]. } # Große kreisangehörige Stadt
       UNION
            { ?item p:P31 [ ps:P31 wd:Q54935786 ]. } # Mittlere kreisangehörige Stadt
       UNION
           { ?item p:P31 [ ps:P31 wd:Q1852178 ] . } # Stadteil von Düsseldorf
       UNION
           { ?item p:P31 [ ps:P31 wd:Q15632166 ] . } # Stadtteil von Köln
       UNION
           { ?item p:P31 [ps:P31 wd:Q1780389 ] } # Kommunalverband der besonderen Art (derzeit nur "Städteregion Aachen")
       UNION
           { ?item wdt:P31/wdt:P279*  wd:Q4286337 } # Stadtbezirk, für Geocache auskommentieren
       UNION
           { ?item p:P31 [ ps:P31 wd:Q4286337 ] . }
  SERVICE wikibase:label {  bd:serviceParam wikibase:language "de" }
  OPTIONAL { ?item wdt:P227 ?gnd }
  OPTIONAL { ?item wdt:P576 ?dissolutionDate }
  OPTIONAL { ?item wdt:P150 ?hasPart . }
  OPTIONAL { ?item wdt:P131 ?partOf }
}

GROUP BY ?item ?itemLabel ?partOf ?gnd ?dissolutionDate